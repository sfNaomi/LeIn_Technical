/**
 * Created by magdalena.stanciu on 07.11.2022.
 */

public with sharing class InvoiceGenerationService {
    @TestVisible private static List<aforza__Invoice__c> retrievedInvoices {get; set;}
    @TestVisible private static Map<Id, Id> orderIdSignatureDocumentId {get; set;}

    private List<Invoice> generatedInvoices {get; set;}

    public InvoiceGenerationService(List<Id> invoiceIds) {
        init(invoiceIds);
    }

    /**
     * @description initializes the service
     *
     * @param invoiceIds list of invoice ids
     *
     * @return N/A
     *
     * @author Magdalena Stanciu
     * @date 2022-10-18
     */
    @TestVisible
    private void init(List<Id> invoiceIds) {
        getInvoices(invoiceIds);

        // get order ids to retrieve signatures
        Set<Id> orderIds = new Set<Id>();
        for (aforza__Invoice__c newInvoice : retrievedInvoices) {
            orderIds.add(newInvoice.aforza__Order__c);
        }
        orderIds.remove(null);

        getSignaturesForInvoices(orderIds);
    }

    /**
     * @description retrieves the invoices generated by service
     *
     * @param N/A
     *
     * @return List<Invoice> list of generated invoices
     *
     * @author Magdalena Stanciu
     * @date 2022-11-07
     */
    @TestVisible
    public List<Invoice> generateData() {
        this.generatedInvoices = new List<Invoice>();
        for (aforza__Invoice__c retrievedInvoice : retrievedInvoices) {
            this.generatedInvoices.add(new Invoice(retrievedInvoice));
        }
        return this.generatedInvoices;
    }

    /**
    * @description retrieves invoices for the order ids passed as input parameter
    *
    * @param invoiceIds list of invoice ids
    *
    * @return N/A
    *
    * @author Magdalena Stanciu
    * @date 2022-11-07
    */
    @TestVisible
    private void getInvoices(List<Id> invoiceIds) {
        retrievedInvoices = [
            SELECT
                Id, Name, CreatedDate, InvoiceType__c, PaymentTerms__c, aforza__Due_Date__c, PoNumber__c,
                PaymentInstruction__c, PrintName__c, aforza__Order__c, aforza__Order__r.OrderNumber,
                CustomerShopNumber__c, NetTotal__c, Vat__c, Gross__c, DiscountTotal__c,
                DpReference__c, DpName__c, DpAddress__c, DpArea__c, DpTown__c, DpCountry__c, DpPostCode__c,
                BillToReference__c, BillToName__c, BillToAddress__c, BillToArea__c, BillToTown__c, BillToCountry__c, BillToPostCode__c,
                (
                    SELECT Id, SkuCode__c, SkuName__c, Quantity__c, UnitPrice__c, VatRate__c, Vat__c, NetPrice__c
                    FROM aforza__Billing_Statement_Line_Items__r
                    WHERE SkuName__c NOT IN ('Tax', 'Discount', 'Promotion')
                )
            FROM aforza__Invoice__c
            WHERE Id IN :invoiceIds
        ];
    }

    /**
    * @description retrieves signatures for order ids passed as input parameter
    *
    * @param orderIds list of order ids
    *
    * @return N/A
    *
    * @author Magdalena Stanciu
    * @date 2022-11-07
    */
    @TestVisible
    private void getSignaturesForInvoices(Set<Id> orderIds) {
        if (orderIds.isEmpty()) {
            return;
        }

        List<ContentDocumentLink> fileLinks = [
            SELECT Id, LinkedEntityId, ContentDocument.LatestPublishedVersionId
            FROM ContentDocumentLink
            WHERE LinkedEntityId IN :orderIds AND ContentDocument.Title LIKE 'Signature%'
        ];

        orderIdSignatureDocumentId = new Map<Id, Id>();
        for (ContentDocumentLink link : fileLinks) {
            orderIdSignatureDocumentId.put(link.LinkedEntityId, link.ContentDocument.LatestPublishedVersionId);
        }
    }

    /**
    * @description defines a wrapper class used to expose data to VF page for invoice rendering
    *
    * @author Magdalena Stanciu
    * @date 2022-11-07
    */
    public class Invoice {
        public String billToName {get; set;}
        public String billToReference {get; set;}
        public String billToCountry {get; set;}
        public String billToStreet {get; set;}
        public String billToState {get; set;}
        public String billToCity {get; set;}
        public String billToPostalCode {get; set;}

        public String shipToName {get; set;}
        public String shipToReference {get; set;}
        public String shipToCountry {get; set;}
        public String shipToStreet {get; set;}
        public String shipToState {get; set;}
        public String shipToCity {get; set;}
        public String shipToPostalCode {get; set;}

        public String invoiceNumber {get; set;}
        public String type { get; set;}
        public Datetime createdDate {get; set;}
        public Date dueDate {get; set;}
        public String poNumber {get; set;}
        public String customerShopNumber {get; set;}
        public String paymentMethod {get; set;}

        public Decimal netValue {get; set;}
        public Decimal vatValue {get; set;}
        public Decimal discountTotal {get; set;}
        public Decimal total { get; set;}
        public String paymentTerms {get; set;}

        public String printName {get; set;}
        public Id signature {get; set;}

        public List<InvoiceItem> invoiceItems {get; set;}
        public VatDistribution vatDistribution {get; set;}

        public Invoice(aforza__Invoice__c newInvoice) {
            this.billToName = newInvoice.BillToName__c;
            this.billToReference = newInvoice.BillToReference__c;
            this.billToCountry = newInvoice.BillToCountry__c;
            this.billToStreet = newInvoice.BillToAddress__c;
            this.billToState = newInvoice.BillToArea__c;
            this.billToCity = newInvoice.BillToTown__c;
            this.billToPostalCode = newInvoice.BillToPostcode__c;

            this.shipToName = newInvoice.DpName__c;
            this.shipToReference = newInvoice.DpReference__c;
            this.shipToCountry = newInvoice.DpCountry__c;
            this.shipToStreet = newInvoice.DpAddress__c;
            this.shipToState = newInvoice.DpArea__c;
            this.shipToCity = newInvoice.DpTown__c;
            this.shipToPostalCode = newInvoice.DpPostcode__c;

            this.invoiceNumber = newInvoice?.aforza__Order__r?.OrderNumber == null ? newInvoice.Name : newInvoice?.aforza__Order__r?.OrderNumber;
            this.type = newInvoice.InvoiceType__c;
            this.createdDate = newInvoice.CreatedDate;
            this.dueDate = newInvoice.aforza__Due_Date__c;
            this.poNumber = newInvoice.PoNumber__c;
            this.customerShopNumber = newInvoice.CustomerShopNumber__c;
            this.paymentMethod = newInvoice.PaymentInstruction__c;
            this.paymentTerms = newInvoice.PaymentTerms__c;

            this.netValue = newInvoice.NetTotal__c;
            this.vatValue = newInvoice.Vat__c;
            this.discountTotal = newInvoice.DiscountTotal__c;
            this.total = newInvoice.Gross__c;

            this.printName = newInvoice.PrintName__c;
            this.signature = orderIdSignatureDocumentId?.get(newInvoice.aforza__Order__c);

            this.invoiceItems = new List<InvoiceItem>();
            this.vatDistribution = new VatDistribution();
            for (aforza__Invoice_Line_Item__c item : newInvoice.aforza__Billing_Statement_Line_Items__r) {
                this.invoiceItems.add(new InvoiceItem(item));
                this.vatDistribution.add(item);
            }
        }
    }

    /**
    * @description defines a wrapper class used to expose data to VF page for invoice rendering
    *
    * @author Magdalena Stanciu
    * @date 2022-11-07
    */
    public class InvoiceItem {
        public String code {get; set;}
        public String description {get; set;}
        public Integer quantity {get; set;}
        public Decimal unitPrice {get; set;}
        public Decimal vatRate {get; set;}
        public Decimal vatValue {get; set;}
        public Decimal netPrice {get; set;}

        public InvoiceItem(aforza__Invoice_Line_Item__c newItem) {
            this.code = newItem.SkuCode__c;
            this.description = newItem.SkuName__c;
            this.quantity = (Integer) newItem.Quantity__c;
            this.unitPrice = newItem.UnitPrice__c;
            this.vatRate = newItem.VatRate__c;
            this.vatValue = newItem.Vat__c;
            this.netPrice = newItem.NetPrice__c;
        }
    }

    /**
    * @description defines a wrapper class used to expose data to VF page for invoice rendering
    *
    * @author Magdalena Stanciu
    * @date 2022-11-07
    */
    public class VatDistribution {
        private Map<Decimal, VatItem> items {get; set;}

        public List<VatItem> vatItems {
            get {
                return this.items.values();
            }
            set;
        }

        public VatDistribution() {
            this.items = new Map<Decimal, VatItem>();
        }

        public void add(aforza__Invoice_Line_Item__c newInvoiceItem) {
            VatItem newVatItem = items.get(newInvoiceItem.VatRate__c);
            if (newVatItem == null) {
                newVatItem = new VatItem(newInvoiceItem);
            } else {
                newVatItem.netValue += newInvoiceItem.NetPrice__c == null ? 0 : newInvoiceItem.NetPrice__c;
                newVatItem.vatValue += newInvoiceItem.Vat__c == null ? 0 : newInvoiceItem.Vat__c;
            }
            items.put(newInvoiceItem.VatRate__c, newVatItem);
        }
    }

    /**
    * @description defines a wrapper class used to expose data to VF page for invoice rendering
    *
    * @author Magdalena Stanciu
    * @date 2022-11-07
    */
    public class VatItem {
        public Decimal netValue {get; set;}
        public Decimal vatRate {get; set;}
        public Decimal vatValue {get; set;}

        public VatItem(aforza__Invoice_Line_Item__c newItem) {
            this.netValue = newItem.NetPrice__c == null ? 0 : newItem.NetPrice__c;
            this.vatRate = newItem.VatRate__c == null ? 0 : newItem.VatRate__c;
            this.vatValue = newItem.Vat__c == null ? 0 : newItem.Vat__c;
        }
    }
}